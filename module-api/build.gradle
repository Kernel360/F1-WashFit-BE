buildscript {
    ext {
        restdocsApiSpecVersion = '0.17.1'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}"
    id 'org.hidetake.swagger.generator' version '2.18.2'
}

group = 'com.kernel360'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':module-common')
    implementation project(':module-domain')

    // spring-boot-starter
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // jasypt
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.5'

    // flyway
    implementation 'org.flywaydb:flyway-core'

    // postgresql
    runtimeOnly 'org.postgresql:postgresql'

    //lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // validataion
    implementation 'org.springframework.boot:spring-boot-starter-validation:2.7.4'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // test - fixture Monkey
    testImplementation 'net.jqwik:jqwik:1.7.3'
    testImplementation("com.navercorp.fixturemonkey:fixture-monkey-starter:1.0.0")
    testImplementation("com.navercorp.fixturemonkey:fixture-monkey-jakarta-validation:1.0.0")

    // rest docs & swagger
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:' + restdocsApiSpecVersion
    swaggerUI 'org.webjars:swagger-ui:4.11.1'

    //actuator
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    //prometheus
    implementation 'io.micrometer:micrometer-registry-prometheus'

    //mybatis
    implementation group: 'org.mybatis.spring.boot', name: 'mybatis-spring-boot-starter', version: '3.0.3'

    // querydsl
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // badword Filtering
    implementation 'io.github.vaneproject:badwordfiltering:1.0.0'

    // commons-collections4
    implementation 'org.apache.commons:commons-collections4:4.4'

}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(GenerateSwaggerUI) {
    dependsOn 'openapi3'
}

swaggerSources {
    sample {
        setInputFile(file("${layout.buildDirectory.get().asFile}/api-spec/openapi3.yaml"))
    }
}

openapi3 {
    servers = [
        { url = "https://devapi.washfit.site" },
        { url = "http://localhost:8080" }
    ]
    title = "API Document"
    description = "Rest Docs With Swagger"
    version = "0.0.1"
    format = "yaml"
}

bootJar {
    def apiDocNeeded = !project.hasProperty('apiDocNeeded') ? true : apiDocNeeded.toBoolean()

    if (apiDocNeeded) {
        dependsOn generateSwaggerUISample
        from("${generateSwaggerUISample.outputDir}") {
            into 'static/docs'
        }
    }
}

tasks.register("prepareKotlinBuildScriptModel") {}